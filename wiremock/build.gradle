plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id 'jacoco'
    id "com.jfrog.bintray" version '1.6'
    id 'com.github.kt3k.coveralls' version '2.6.3'
}

repositories {
    jcenter()
}

dependencies {
    compile "org.springframework.restdocs:spring-restdocs-core:${rootProject.springRestdocsVersion}"
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'uk.co.datumedge:hamcrest-json:0.2'
    testCompile 'com.google.guava:guava:19.0'
}

group = 'com.epages'
description = 'Adds WireMock stub snippets to Spring REST Docs'
sourceCompatibility = 1.7
targetCompatibility = 1.7

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

tasks.coveralls {
    dependsOn 'check'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId 'restdocs-wiremock'
            groupId = project.group

            artifact sourceJar {
                classifier 'sources'
            }
        }
    }
}

bintray {
    user = project.findProperty('bintrayUser')   ?: System.getenv('BINTRAY_USER')
    key  = project.findProperty('bintrayApiKey') ?: System.getenv('BINTRAY_API_KEY')
    publications = ['mavenJava','sourceJar']
    publish = true
    pkg {
        repo = "maven"
        name = 'restdocs-wiremock'
        userOrg = 'epages'
        websiteUrl = 'https://github.com/ePages-de/restdocs-wiremock'
        issueTrackerUrl = 'https://github.com/ePages-de/restdocs-wiremock/issues'
        vcsUrl = 'https://github.com/ePages-de/restdocs-wiremock.git'
        githubRepo = 'ePages-de/restdocs-wiremock'
        githubReleaseNotesFile = 'README.md'
        licenses = ['Apache-2.0']
    }
}
