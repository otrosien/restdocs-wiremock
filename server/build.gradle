buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.4.2.RELEASE'
    }
}

plugins {
    id "org.asciidoctor.convert" version "1.5.3"
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

repositories {
    mavenLocal()
    jcenter()
}

sourceCompatibility = 1.7
targetCompatibility = 1.7
group = 'com.epages'

ext {
    snippetsDir = file('build/generated-snippets')
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-hateoas'

    runtime 'com.h2database:h2'
    runtime 'org.atteo:evo-inflector:1.2.1'

    testCompile 'com.jayway.jsonpath:json-path'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testCompile "com.epages:restdocs-wiremock:${project.version}"
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    attributes 'snippets': snippetsDir
    inputs.dir snippetsDir
    dependsOn test
}

jar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

task wiremockJar(type: Jar) {
    description = 'Generates the jar file containing the wiremock stubs for your REST API.'
    group = 'Build'
    classifier = 'wiremock'
    dependsOn project.tasks.test
    from (snippetsDir) {
        include '**/wiremock-stub.json'
        into "wiremock/${project.name}/mappings"
    }
}

model {
    publishing {
        publications {
            wiremockJar(MavenPublication) {
                from project.components.java
                artifact project.tasks.wiremockJar {
                    classifier "wiremock"
                }
            }
        }
    }
}

eclipseJdt.onlyIf { false }
cleanEclipseJdt.onlyIf { false }
assemble.dependsOn(':restdocs-wiremock:publishToMavenLocal')
compileJava.dependsOn(':restdocs-wiremock:publishToMavenLocal')
